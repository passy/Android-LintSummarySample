task hasLintSummaryExecutable(type: Exec) {
    commandLine "which", "android-lint-summary"
    ignoreExitValue true

    ext.hasExecutable = {
        execResult?.getExitValue() == 0
    }
}

task lintSummary(type: LintSummaryTask, dependsOn: hasLintSummaryExecutable) {
    description "Print a summary of your lint issues"
    group JavaBasePlugin.VERIFICATION_GROUP

    outputs.upToDateWhen { false }
    onlyIf { gradle.startParameter.colorOutput && hasLintSummaryExecutable.hasExecutable() }
}


afterEvaluate {
    tasks.withType(tasks['lint'].class) {
        finalizedBy lintSummary
    }

    gradle.addBuildListener lintSummary
}

class LintSummaryTask extends DefaultTask implements BuildListenerAdapter {
    def stdout = new ByteArrayOutputStream()

    @SuppressWarnings("GroovyUnusedDeclaration")
    @TaskAction
    public def action() {
        def filePath = [project.projectDir, "build", "outputs", "lint-results.xml"].join(File.separator)
        project.exec {
            commandLine "android-lint-summary", "-v", filePath
            standardOutput = stdout
        }
    }

    @Override
    void buildFinished(BuildResult result) {
        println stdout.toString()

        if (state.skipped) {
            println """
For prettier display of lint errors, install `android-lint-summary`.

echo "/home/phartig/tools/android-lint-summary" >> ~/.tools
synd-dottools.sh

Learn more at go/als.
            """.stripMargin()
        }
    }
}

trait BuildListenerAdapter implements BuildListener {
    @Override
    void buildFinished(BuildResult result) {

    }

    @Override
    void buildStarted(Gradle gradle) {

    }

    @Override
    void projectsEvaluated(Gradle gradle) {

    }

    @Override
    void projectsLoaded(Gradle gradle) {

    }

    @Override
    void settingsEvaluated(Settings settings) {

    }
}
